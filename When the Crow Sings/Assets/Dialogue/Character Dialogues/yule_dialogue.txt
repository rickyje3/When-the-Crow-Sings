~ yule_directory
if YuleBaseCompleted == false:
	=> yule_directory_2
else:
	=> yule_beyond_base

~ yule_directory_2
if YuleTaskOffered == false:
	=> yule_first_int
if YuleTaskAccepted == false:
	=> yule_no_task
else:
	=> yule_directory_3

~ yule_directory_3
if YuleTaskPartCompleted == false:
	=> yule_task_part_unfin
else:
	=> yule_directory_4

~ yule_directory_4
if YuleTaskCompleted == true:
	=> yule_task_fin
if YuleTaskTwoOn == false:
	=> yule_task_part_fin
else:
	=> yule_task_unfin

~yule_first_int
set YuleTaskOffered = true;
Yule: . . .
Chance: Uh, hi.
Yule: Hi.
Chance: . . .
Yule: . . .
Chance: Um, <color=#813408>Quinn</color> sent me?
Yule_Stare: I know.
Chance: Right, um. Did they tell you what I wanted to talk about?
Yule: Pagers have a short message limit.
Chance_Sad: Oh. Er, okay. Well, I wanted to ask about the people who roam around at night, in the center. Do you know anything about that?
Yule_Thinking: Yes, I know about it.
Chance: . . . Could you tell me about it?
Yule: Sure, I can. Not right now, though. I'm busy.
Chance: Doing what, exactly?
Yule_Stare: Fishing.
Chance: Uh, with what?
Yule: A rod, ideally.
Chance: I don't see one.
Yule_Sad: I don't have one.
Chance: . . . Do you need one?
Yule_Sad: Yep. That's the problem.
Chance: What happened to your old one?
Yule: An unruly fish took it from me years ago. I've been net fishing since, but the urge beset upon me once again.
Chance: So.. If you get one, will you no longer be busy?
Yule_Thinking: That makes sense.
Chance: Do you want me to get you one?
Yule_Stare: Do you want to get me one?
- Yes.
	set MFYuleTag = false;
	set HFYuleTA = true;
	set YuleTaskAccepted = true;
	set YuleTaskOneOn = true;
	Chance: If it will make you not busy, then yes.
	Yule: If you say so. 
	Chance: Uh, what do I need to make you a fishing rod?
	Yule_Thinking: Oh let's see.. A young flexible branch for the rod, some wire for the guides, and some fiber for line.
	Chance: I can find all that here?
	Yule: Should be able to.
	Chance: Okay, I'll see what I can do.
	set MFYuleRod = true;
	set MFYuleString = true;
	set MFYuleHook = true;
	emit 1;
	=> END
- No.
	Chance: No, not right now.
	Yule: Okay.
	Chance: . . .
	Yule: . . .
	=> END

~yule_no_task
Yule: You're back.
Chance: Yeah, I wanted to talk to you about what I mentioned before.
Yule: Once I'm done fishing.
- I'll help.
	set MFYuleTag = false;
	set HFYuleTA = true;
	set YuleTaskAccepted = true;
	set YuleTaskOneOn = true;
	Chance: I'll help you, what do I need to make you a fishing rod?
	Yule_Thinking: Oh let's see.. A young flexible branch for the rod, some wire for the guides, and some fiber for line.
	Chance: I can find all that here?
	Yule: Should be able to.
	Chance: Okay, I'll see what I can do.
	set MFYuleRod = true;
	set MFYuleString = true;
	set MFYuleHook = true;
	emit 1;
	=> END
- Right.
	=> END

~ yule_task_part_unfin
Yule: Mm. You're back.
Chance: Yup.
Yule_Thinking: Why?
- What was I doing?
	Chance: What was I doing, again?
	Yule_Smile: You were getting the ingredients for a fishing rod.
	Chance_Smile: Ahh, right. Thanks!
	Yule: Mm..
	=> END
- No reason.
	Chance: No reason.
	Yule: Okay.
	Chance: . . .
	Yule: . . . 
	=> END

~ yule_task_part_fin
set YuleTaskTwoOn = true;
Chance: Alright, I got all the stuff you asked for.
Yule_Smile: It looks good. Let me have it.
Yule_Stare: . . .
Chance: Uh, isn't it for you?
Yule_Thinking: Yes. But I don't know if it works.
Chance: ... So you want me to see if it works?
Yule: Yes.
Chance: Uh, okay.
set YuleFishingrod = true;
set MFYuleTPC = false;
set MFYuleTNC = true;
set HFYuleTNC = true;
emit 1;
=> END

~ yule_task_unfin
set MFYuleTPC = false;
Yule_Smile: You went through all the trouble to get the pieces for the rod, don't keep me in suspense. Catch a fish!
=> END

~ yule_task_fin
set MFYuleTPC = false;
set KeyInformation1 = true;
set YuleBaseCompleted = true;
set MFYuleTC = false;
Yule_Smile: That's a nice looking fish. Came up easy, too. It's a good rod.
Chance: Yeah, it is.
It made me feel like when I used to fish at home.
Yule_Sad: So, you wanted to know about the ones that walk around in the compound at night.
Chance: Yeah, if you could tell me. 
Yule_Thinking: I don't really know much about them.
Chance: What? But you said you could tell me something.
Yule: I can. Just not about one, specifically. But I know where they come from.
Yule_Stare: When this compound was established, it was based around <color=#a11bbe>a discovery</color> that had been made. I couldn't tell you what, I don't know much about isotopes or elements or anything like that.
Yule_Sad: But there was <color=#813408>a scientist</color> and <color=#813408>his assistant</color>. They were the reason it was set up, he had discovered this <color=#a11bbe>energy producing anomaly</color> so they built the compound around it.
Yule: Well, not around it physically. 'Far as I know it's a ways off, to the north. But that's why we were brought here. Me and a smattering of other folks. Families, friends. This was shortly after a few other collapses but, it was a while ago. Don't remember the names.
Yule_Stare: Things were fine at first, but pretty quick some folk started going backwards. You know what I mean.
I do.
Yule_Sad: Well they sent us that boy <color=#813408>Francisco</color> and they thought they could keep a handle on it. But people started to slip so far back that they couldn't handle it anymore. Started to get wild. Usually they just ran off before anything happened, but every once in a while something was.. <color=#a11bbe>wrong</color>.
Yule: See, that syndrome us EVOs get usually just backslides you down the evolutionary line, but these people didn't do that. They got <color=#a11bbe>different</color>. 
Yule_Stare: Grew new bits, started hacking up this awful purple pink slime.  After a new batch of people came, that <color=#813408>scientist</color> ran off.
if PhilomenaBaseCompleted == true:
	Chance: And he left <color=#813408>Phil</color> here?
	Yule_Sad: I suppose he did... Poor woman.
Yule_Stare: That day, the power went out and that grid stopped workin'. Coalition sent out new parts, new people, and everything was getting better. Haven't seen much of the purple ones since. Well, till now.
Yule: And that thing still kicks on at night, humming and glowing. And the plants to the north got that sickly color to 'em.
Yule_Angry: Somehow I feel like it's that bastard's fault this all happened. I don't quite know, but I think that <color=#a11bbe>energy source</color> got into 'em. Maybe they woulda backslid, but they'd be normal. 
Chance: Wow.
Chance: You sure talk a lot once you get the chance.
Yule_Smile: Hah! Well, you know what they say about bears.
Chance: What do they say?
Yule_Smile: I was hoping you knew.
Chance_Excited: Pfff!
Chance_Smile: Thanks for telling me all of that. I'm not sure if I'll be able to do anything about it, but I'm sure there's something in all of that that I might be able to use to stop this.
Yule_Thinking: Stop it?
Chance: Yeah I-
Chance_Sad: I lost my last home. I don't want it to happen again.
Yule_Sad: . . .
Chance: . . .
Yule_Thinking: His name was <color=#a11bbe>Simon</color>. The scientist. <color=#a11bbe>Simon Petrova</color>. 
Chance: Yeah?
Yule_Angry: Yes. Wherever he ran off to, I hope he doesn't get a good night's sleep for the rest of his life.
Chance: Yeah...
Chance: Thanks for the info, big guy.
Yule: Sure. Come and see me again, if you want.
set HFYuleBC = true;
emit 1;
set timeOfDay += 1;
call ReloadScene(1);
=> END

~ yule_beyond_base
This guy doesn't talk much, I'm gonna fuck off.
=> END